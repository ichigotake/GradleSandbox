apply plugin: 'android'

enum BuildType {
    RELEASE("", "main"), ALPHA("alpha", "alpha"), DEBUG("debug", "debug")

    final private mName;
    final private mDirName;

    private BuildType(String name, String dirName) {
        mName = name;
        mDirName = dirName;
    }

    public static BuildType get(String name) {
        for (BuildType type : values()) {
            if (type.toString() == name) {
                return type;
            }
        }
        return DEBUG;
    }

    public String getDirName() {
        return mDirName;
    }

    @Override
    public String toString() {
        return mName;
    }
}

// ビルドの種類によって処理を振り分ける
// gradle-android-utils: writeResources(File, Map<String, String>)
// TODO `gradle.properties`を使わずにアプリ名を取得したい // Context#getString(int resid)したい
android.buildTypes.all{theBuildType ->
    def buildType = BuildType.get(theBuildType.name)
    def resourceDir = new File("app/src/${buildType.getDirName()}/res/values/")
    resourceDir.mkdirs()
    def resourceFile = new File(resourceDir, "_app.xml")

    writeResources(resourceFile, ["app_name": appName + " " + buildType.toString()])
}


android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 19


        // gradle-android-utils: readVersion()
        def version = readVersion(file("VERSION"))
        versionCode version.toVersionCode()
        versionName version.toVersionName()
    }

    // メタファイルが原因で同じパッケージが複数コンパイルされてコケる場合がある
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }

    // 署名ビルド
    signingConfigs {
        release {
            storeFile file(project.properties.storeFile)
            storePassword project.properties.storePassword
            keyAlias project.properties.storeAlias
            keyPassword project.properties.keyPassword
        }
    }

    // ビルドの種類。括弧内の環境は一例
    // 指定方法
    //  AndroidStudio:「Build Variants」
    //  ./gradlew: 基本形として、タスク名の末尾にBuild Variantsをつける
    //              利用出来るタスクの一覧は「Gradle」ウィンドウで確認可能
    //              例) ./gradlew assembleStaging
    buildTypes {

        // 本番(Playストア)
        release {
            debuggable false
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }

        // ステージング(Playストア アルファ版)
        alpha.initWith(buildTypes.release) // releaseを継承する
        alpha {
            debuggable true
            runProguard false
            // 端末内に別ビルドのアプリを共存出来るようにするために違うパッケージ名にする
            // AndroidManifest.xmlに<provider>がある場合は、authoritiesをビルド毎に異なる値にする必要がある
            packageNameSuffix "." + BuildType.ALPHA.toString()
            versionNameSuffix "-" + BuildType.ALPHA.toString()
        }

        // デバッグ(開発環境)
        debug {
            debuggable true
            runProguard false
            packageNameSuffix "." + BuildType.DEBUG.toString()
            versionNameSuffix "-" + BuildType.DEBUG.toString()
        }
    }

    // Lintチェック。ブロック内は公式ガイドラインからの引用
    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        //quiet true
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        //ignoreWarnings true
        // if true, emit full/absolute paths to files with errors (true by default)
        //absolutePaths true
        // if true, check all issues, including those that are off by default
        //checkAllWarnings true
        // if true, treat all warnings as errors
        //warningsAsErrors true
        // turn off checking the given issue id's
        //disable 'TypographyFractions','TypographyQuotes'
        // turn on the given issue id's
        //enable 'RtlHardcoded','RtlCompat', 'RtlEnabled'
        // check *only* the given issue id's
        //check 'NewApi', 'InlinedApi'
        // if true, don't include source code lines in the error output
        //noLines true
        // if true, show all locations for an error, do not truncate lists, etc.
        //showAll true
        // Fallback lint configuration (default severities, etc.)
        //lintConfig file("default-lint.xml")
        // if true, generate a text report of issues (false by default)
        //textReport true
        // location to write the output; can be a file or 'stdout'
        //textOutput 'stdout'
        // if true, generate an XML report for use by for example Jenkins
        //xmlReport false
        // file to write report to (if not specified, defaults to lint-results.xml)
        //xmlOutput file("lint-report.xml")
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        //htmlReport true
        // optional path to report (default will be lint-results.html in the builddir)
        //htmlOutput file("lint-report.html")
    }
}

repositories {
    // 野良Mavenレポジトリ
    maven { url 'https://raw.github.com/ichigotake/ColorfulSweets/develop/repository' }
}

dependencies {
    compile 'com.android.support:support-v4:19.0.0'
    compile 'com.android.support:appcompat-v7:+'
    // 野良Mavenレポジトリ
    compile 'net.ichigotake.colorfulsweets:ColorfulSweets:0.0.6'
    // settings.gradleの別プロジェクトを読み込む
    compile project(':lib')
}
